plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    id 'jacoco'
}

android {
    compileSdk 34
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }
    kotlinOptions {
        jvmTarget = '21'
    }
    buildFeatures.buildConfig = true
    namespace = 'com.l8chat.imsdk'

    // 允许单元测试中使用 Android 类的默认实现
    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
                excludes = ['jdk.internal.*']
            }
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
    }
}

configurations {
    // 创建一个新的配置用于嵌入依赖
    embedded
    implementation.extendsFrom(embedded)
}

dependencies {
    implementation platform("org.jetbrains.kotlin:kotlin-bom:$kotlin_version")
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    
    // 使用 embedded 配置来包含需要打包进 AAR 的依赖
    embedded files('libs/xSocket-2.8.15.jar')
    embedded "net.zetetic:sqlcipher-android:4.9.0"
//    embedded "net.zetetic:android-database-sqlcipher:4.5.4"
    embedded "androidx.sqlite:sqlite-ktx:2.5.1"
    embedded 'org.whispersystems:curve25519-android:0.5.0'
    embedded 'org.whispersystems:signal-protocol-android:2.8.1'
    
    // multidex 通常不需要打包进去，保持 implementation
    implementation 'com.android.support:multidex:1.0.3'

    // 测试依赖
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'com.l8chat'
                artifactId = 'l8-imsdk-android'
                version = project.findProperty("publishVersion") ?: '1.0.0'

            // 配置 pom 以包含传递依赖
            pom {
                name = 'L8 IM Android SDK'
                description = 'L8 IM Android SDK - 即时通讯 SDK'
                url = 'https://github.com/L8CHAT/l8-imsdk-android'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'l8chat'
                        name = 'L8CHAT'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/L8CHAT/l8-imsdk-android.git'
                    developerConnection = 'scm:git:ssh://github.com/L8CHAT/l8-imsdk-android.git'
                    url = 'https://github.com/L8CHAT/l8-imsdk-android'
                }
            }
        }
    }
    repositories {
        maven {
            name = 'localRepo'
            url = uri("${project.rootDir}/repository")
        }
        maven {
            name = 'GitHubPackages'
            url = uri("https://maven.pkg.github.com/L8CHAT/l8-imsdk-android")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}}

android.libraryVariants.all { variant ->
    def name = variant.name.capitalize()
    task("generate${name}Javadoc", type: Javadoc) {
        description = "Generates Javadoc for $variant.name."
        source = variant.sourceSets.collect { it.java.sourceFiles }.inject { m, i -> m + i }
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        classpath += configurations.implementation
        options.addStringOption('Xdoclint:none', '-quiet')
        options.addStringOption('encoding', 'UTF-8')
        options.addStringOption('charSet', 'UTF-8')
    }
}

// JaCoCo 覆盖率报告任务
task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest']) {
    reports {
        xml.required = true
        html.required = true
    }

    def fileFilter = [
        '**/R.class',
        '**/R$*.class',
        '**/BuildConfig.*',
        '**/Manifest*.*',
        '**/*Test*.*',
        'android/**/*.*'
    ]

    def debugTree = fileTree(dir: "${buildDir}/intermediates/javac/debug/compileDebugJavaWithJavac/classes", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories.setFrom(files([mainSrc]))
    classDirectories.setFrom(files([debugTree]))
    executionData.setFrom(fileTree(dir: "$buildDir", includes: [
        "outputs/unit_test_code_coverage/debugUnitTest/testDebugUnitTest.exec",
        "jacoco/testDebugUnitTest.exec"
    ]))
}

